grammar avroclipse.AvroIDL with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate avroIDL "http://avroclipse/AvroIDL"

AvroIDLFile:
	{AvroIDLFile}
	(
		(=> '@namespace' '(' name=STRING ')')? & //TODO: must be in QN format (validation) '-' '_' and ' ' are not allowed
		(annotations+=Annotation)*
	)
	('protocol' protocolName=EscapableId '{'
		imports+=Import*
		(types+=TypeDef | messages+=RPCMessage)*
	'}')?;
	
Import:
	'import' type=ImportType importURI=STRING ';';
	
RPCMessage:
	returnType=ReturnTypeLink name=EscapableId '(' (arguments+=Argument (',' arguments+=Argument)*)* ')' ('throws'
	error=[ErrorType|QN])? (oneway?='oneway')? ';';

Argument:
	type=TypeLink name=EscapableId ('=' default=Value)?;

ReturnTypeLink:
	VoidTypeLink | TypeLink | ArrayFieldType | MapFieldType;

VoidTypeLink:
	target='void';

TypeDef:
	annotations+=Annotation* type=Type;

Type:
	EnumType | FixedType | RecordType | ErrorType;

ErrorType:
	'error' name=EscapableId '{'
		fields+=Field*
	'}';

RecordType:
	'record' name=EscapableId '{'
		fields+=Field*
	'}';

Field:
	annotations+=Annotation* type=FieldType nameAnnotations+=Annotation* name=EscapableId ('=' default=Value)? ';';

FieldType:
	SimpleFieldType | UnionFieldType | ArrayFieldType | MapFieldType;

SimpleFieldType:
	type=TypeLink;
	
UnionFieldType:
	'union' '{' types+=TypeLink (',' types+=TypeLink)* '}';

ArrayFieldType:
	'array' '<' annotation=Annotation? type=TypeLink '>';

MapFieldType:
	'map' '<' annotation=Annotation? type=TypeLink '>';

TypeLink:
	PrimativeTypeLink | CustomTypeLink;

PrimativeTypeLink:
	target=('int' | 'long' | 'string' | 'boolean' | 'float' | 'double' | 'null' | 'bytes');

CustomTypeLink:
	target=[Type|QN];

FixedType:
	'fixed' name=EscapableId '(' size=INT ')' ';';

EnumType:
	'enum' name=EscapableId '{'
		literals+=ID (',' literals+=ID)*
	'}';

Annotation:
	name=AN ('(' values=Values ')')?;

Values:
	{Values}
	(value+=Array | value+=Value (',' value+=Value)*)*;

Array:
	=> '[' values=Values ']';

Value:
	StringValue | Array | IntValue | BooleanValue | Null;

BooleanValue:
	val=Boolean;

Null:
	val='null';

StringValue:
	val=STRING;

IntValue:
	val=(INT|NegativeInt);
	
QN:
	EscapableId ('.' EscapableId)*;

EscapableId:
	ID | '`' ReservedKeyWords '`';

ReservedKeyWords:
	'record' | 'fixed' | 'enum' | 'map' | 'array' | 'union' | 'error' | 'protocol' | 'oneway' | 'import' | 'throws';

AN:
	'@' ID ('-' ID)*;
	
NegativeInt returns ecore::EInt:
	'-' INT;
	
Boolean returns ecore::EBoolean: 'true' | 'false';

enum ImportType:
	IDL='idl' |
	PROTOCOL='protocol' |
	SCHEMA='schema';
